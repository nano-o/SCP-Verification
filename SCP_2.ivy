#lang ivy1.7

include order

type node
relation intact(N:node)

type quorum

relation member(N:node, Q:quorum)
type bset # blocking sets
relation bset_member(N:node, B:bset)
relation blocking(N:node, B:bset)

instance value : bounded_sequence(nat)
instance round : bounded_sequence(nat)

type ballot = struct {
    n:round,
    x:value
}

# TODO: this does not seem to work:
# type statement = {abort, commit}
type statement
individual commit:statement
individual abort:statement
trusted isolate commit_abort_iso = {
    property commit ~= abort
    property X = commit | X = abort 
}

trusted isolate fbas_axiomatization = {
    axiom (exists N1 . intact(N1) & member(N1,Q1)) & (exists N2 . intact(N2)  & member(N2,Q2)) -> exists N3 . intact(N3) & member(N3,Q1) & member(N3,Q2)
    axiom (exists N . intact(N) & blocking(N,BL)) -> exists N2 . intact(N2) & bset_member(N2,BL)
}

isolate ballot_order = {
    definition (B1 < B2) = (n(B1) < n(B2)) | (n(B1) = n(B2) & x(B1) < x(B2))
    definition (B1:ballot <= B2) = (B1 < B2 | B1 = B2)
    instantiate totally_ordered(ballot)
} with value, round


isolate simple_ballot_protocol = { # just a warm-up
    
    relation vote(N:node, B:ballot, S:statement)
    relation accept(N:node, B:ballot, S:statement)
    relation confirm(N:node, B:ballot, S:statement)

    # ghost vars (do not affect non-ghost vars):
    individual accepted_commit:bool
    individual the_b:ballot

    ## Invariants
    # note that invariants do not say anything about ill-behaved or befouled nodes; therefore we do not bother to model their behavior

    invariant [safety] ~(intact(N1) & intact(N2) & confirm(N1,B1,commit) & confirm(N2,B2,commit) & x(B1) ~= x(B2))

    private { # together with safety, the following invariants form an inductive invariant
        # the main invariant:
        invariant (exists N . intact(N) & vote(N,B2,commit)) & B1 < B2 & x(B1) ~= x(B2) ->
            (exists N . intact(N) & accept(N,B1,abort)) | (accepted_commit & the_b <= B1 & x(the_b) = x(B2))

        # invariants about the ghost vars
        invariant accepted_commit -> (exists N . intact(N) & accept(N,the_b,commit))
        invariant intact(N) & accept(N,B,commit) -> accepted_commit & the_b <= B 

        # an intact node does not cast contradictory votes
        invariant ~(intact(N) & vote(N,B,S1) & vote(N,B,S2) & S1 ~= S2)

        # federated_voting invariants:
        invariant ~(intact(N1) & intact(N2) & accept(N1,B1,S1) & accept(N2,B1,S2) & S1 ~= S2)
        invariant ~(intact(N1) & intact(N2) & confirm(N1,B1,S1) & confirm(N2,B1,S2) & S1 ~= S2)
        invariant (exists N1 . intact(N1) & accept(N1,B1,S1)) -> exists Q . (exists N3 . intact(N3) & member(N3,Q)) & (forall N2 . intact(N2) & member(N2, Q) -> vote(N2,B1,S1))
        invariant (exists N1 . intact(N1) & confirm(N1,B1,S1)) -> exists Q . (exists N1 . intact(N1) & member(N1,Q)) & forall N2 . intact(N2) & member(N2,Q) -> accept(N2,B1,S1)
    }
    
    after init {
        vote(N,B,S) := false;
        accept(N,B,S) := false;
        confirm(N,B,S) := false;
	accepted_commit := false;
    }

    relation condition_accept(N:node, B:ballot, S:statement)
    definition condition_accept(V:node, B:ballot, S:statement) = 
    (exists Q . member(V,Q) & forall N . member(N,Q) -> (vote(N,B,S) | accept(N,B,S)))
    | (exists BL . blocking(V,BL) & forall N . bset_member(N,BL) -> accept(N,B,S))

    relation condition_vote(N:node, B:ballot, S:statement)
    definition condition_vote(V:node, B:ballot, S:statement) =
    forall S2 . ~(vote(V,B,S2) & S ~= S2)

    relation condition_confirm(N:node, B:ballot, S:statement)
    definition condition_confirm(V:node, B:ballot, S:statement) =
    exists Q . member(V,Q) & forall N . member(N,Q) -> accept(N,B,S)

    ## Basic actions
    
    action vote_abort(n:node, b:ballot) = {
        require condition_vote(n,b,abort);
        vote(n,b,abort) := true;
    }
    
    action vote_commit(n:node, b:ballot) = {
        require B < b & x(B) ~= x(b) -> confirm(n,B,abort);
        require condition_vote(n,b,commit);
        vote(n,b,commit) := true;
    }
    
    action do_accept(v:node, b:ballot, s:statement) = {
        require condition_accept(v,b,s);
        accept(v,b,s) := true;

        # ghost update:
	if (intact(v) & (s = commit & (~accepted_commit | (accepted_commit & b < the_b)))) {
	    accepted_commit := true;
	    the_b := b;
	}
    }

    # Allows to vote without preparing when the value has previously been accepted
    action vote_commit_after_accept(v:node, b:ballot, ab:ballot) = {
        require ab < b & ab.x = b.x & accept(v,ab,commit);
	require condition_vote(v,b,commit);
        vote(v,b,commit) := true;
    }

    action do_confirm(n:node, b:ballot, s:statement) = {
        require condition_confirm(n,b,s);
        confirm(n,b,s) := true;
    }
    
    export vote_abort
    export vote_commit
    export do_accept
    export do_confirm
    export vote_commit_after_accept
    
} with value, round, ballot_order, fbas_axiomatization, commit_abort_iso

isolate abs_actions = {
    
    relation vote(N:node, B:ballot, S:statement)
    relation accept(N:node, B:ballot, S:statement)
    relation confirm(N:node, B:ballot, S:statement)

    # ghost vars (do not affect non-ghost vars):
    individual accepted_commit:bool
    individual the_accepted_b:ballot
    individual confirmed_commit:bool
    individual the_confirmed_b:ballot

    ## Invariants
    # note that invariants do not say anything about ill-behaved or befouled nodes; therefore we do not bother to model their behavior

    invariant [safety] ~(intact(N1) & intact(N2) & confirm(N1,B1,commit) & confirm(N2,B2,commit) & x(B1) ~= x(B2))

    private { # together with safety, the following invariants form an inductive invariant
        
        # the main invariant:
        invariant (exists N . intact(N) & vote(N,B2,commit)) & B1 < B2 & x(B1) ~= x(B2) ->
            (exists N . intact(N) & accept(N,B1,abort)) | (accepted_commit & the_accepted_b <= B1 & x(the_accepted_b) = x(B2))

        # invariants about the ghost vars
        invariant accepted_commit -> (exists N . intact(N) & accept(N,the_accepted_b,commit))
        invariant intact(N) & accept(N,B,commit) -> accepted_commit & the_accepted_b <= B
        invariant confirmed_commit -> (exists N . intact(N) & confirm(N,the_confirmed_b,commit))
        invariant intact(N) & confirm(N,B,commit) -> confirmed_commit & the_confirmed_b <= B

        # an intact node does not cast contradictory votes
        invariant ~(intact(N) & vote(N,B,S1) & vote(N,B,S2) & S1 ~= S2)

        # federated_voting invariants:
        invariant ~(intact(N1) & intact(N2) & accept(N1,B1,S1) & accept(N2,B1,S2) & S1 ~= S2)
        invariant ~(intact(N1) & intact(N2) & confirm(N1,B1,S1) & confirm(N2,B1,S2) & S1 ~= S2)
        invariant (exists N1 . intact(N1) & accept(N1,B1,S1)) -> (
        exists Q . (exists N3 . intact(N3) & member(N3,Q)) & (forall N2 . intact(N2) & member(N2, Q) -> vote(N2,B1,S1)))
        #| (S1 = commit & confirmed_commit & the_confirmed_b < B1 & x(the_confirmed_b) = x(B1))
        
        invariant (exists N1 . intact(N1) & confirm(N1,B1,S1)) -> exists Q . (exists N1 . intact(N1) & member(N1,Q)) & forall N2 . intact(N2) & member(N2,Q) -> accept(N2,B1,S1)
    }
    
    after init {
        vote(N,B,S) := false;
        accept(N,B,S) := false;
        confirm(N,B,S) := false;
	accepted_commit := false;
        confirmed_commit := false;
    }

    relation condition_accept(N:node, B:ballot, S:statement)
    definition condition_accept(V:node, B:ballot, S:statement) = 
    (exists Q . member(V,Q) & forall N . member(N,Q) -> (vote(N,B,S) | accept(N,B,S)))
    | (exists BL . blocking(V,BL) & forall N . bset_member(N,BL) -> accept(N,B,S))

    relation condition_vote(N:node, B:ballot, S:statement)
    definition condition_vote(V:node, B:ballot, S:statement) =
    forall S2 . ~(vote(V,B,S2) & S ~= S2)

    relation condition_confirm(N:node, B:ballot, S:statement)
    definition condition_confirm(V:node, B:ballot, S:statement) =
    exists Q . member(V,Q) & forall N . member(N,Q) -> accept(N,B,S)
    
    action accept_prepared(v:node, p:ballot) = {
        require B < p & x(B) ~= x(p) -> condition_accept(v,B,abort);
        accept(v,B,abort) := accept(v,B,abort) | (B < p & x(B) ~= x(p));
    }
    
    action vote_or_accept_prepared(v:node, b:ballot) = {
	require ~(B < b & x(B) ~= x(b) & ~(condition_vote(v,B,abort) | condition_accept(v,B,abort)));
        vote(v, B, abort) := vote(v, B, abort) | (B < b & x(B) ~= x(b) & condition_vote(v, B, abort));
        accept(v, B, abort) := accept(v, B, abort) | (B < b & x(B) ~= x(b) & condition_accept(v,B,abort));
    }

    action confirm_prepared(v:node, b:ballot) = {
	require ~(B < b & x(B) ~= x(b) & ~condition_confirm(v, B, abort));
        confirm(v, B, abort) := confirm(v, B, abort) | (B < b & x(B) ~= x(b));
    }

    action vote_commit_between(v:node, nc:round, nh:round, y:value) = {
        require nc <= nh;
        var bh:ballot;
        bh.n := nh;
        bh.x := y;
        var bc:ballot;
        bc.n := nc;
        bc.x := y;
	require ~(B < bh & x(B) ~= y & ~condition_confirm(v, B, abort));
        require ~((bc <= B | B <= bh) & x(B) = y & ~condition_vote(v, B, commit));
        vote(v, B, commit) := vote(v, B, commit) | (bc <= B & B <= bh & x(B) = y);
    }

    action accept_committed(v:node, nc:round, nh:round, y:value) = {
        require nc <= nh;
        require nc <= n(B) & n(B) <= nh & x(B) = y -> condition_accept(v,B,commit);
        accept(v, B, commit) := accept(v, B, commit) | (nc <= n(B) & n(B) <= nh & x(B) = y);
        
        # ghost update:
        var b:ballot;
        b.n := nc;
        b.x := y;
	if (intact(v) & (~accepted_commit | (accepted_commit & b < the_accepted_b))) {
	    accepted_commit := true;
	    the_accepted_b := b;
	}
    }

    action vote_or_accept_prepared_all(v:node, b:ballot) = {
	require ~(x(B) ~= x(b) & ~(condition_vote(v,B,abort) | condition_accept(v,B,abort)));
        vote(v, B, abort) := vote(v, B, abort) | (x(B) ~= x(b) & condition_vote(v, B, abort));
        accept(v, B, abort) := accept(v, B, abort) | (x(B) ~= x(b) & condition_accept(v,B,abort));
    }
    
    action vote_commit_all_ge(v:node, b:ballot) = {
        require accept(v, b, commit);
        require ~(b <= B & x(B) = x(b) & vote(v,B,abort));
        vote(v, B, commit) := vote(v, B, commit) | (b <= B & x(B) = x(b));
    }
    
    action do_accept(v:node, b:ballot, s:statement) = {
        require condition_accept(v,b,s);
        accept(v,b,s) := true;

        # ghost update:
	if (intact(v) & (s = commit & (~accepted_commit | (accepted_commit & b < the_accepted_b)))) {
	    accepted_commit := true;
	    the_accepted_b := b;
	}
    }

    action do_confirm(n:node, b:ballot, s:statement) = {
        require condition_confirm(n,b,s);
        confirm(n,b,s) := true;
    }

    action accept_commit_all_ge(v:node, b:ballot) = {
        require confirm(v,b,commit);
        # assert intact(v) -> exists N . intact(N) & accept(N,b,commit);
        # assert intact(v) & intact(N) & b <= B & x(b) ~= x(B) ->  ~vote(N,B,commit);
        # assert intact(v) & intact(N) & b <= B & x(b) ~= x(B) ->  ~accept(N,B,commit);
        # TODO: the following indicates we cannot accept as commited all ballots higher than b:
        assert intact(v) & intact(N) & b <= B & x(b) = x(B) ->  ~accept(N,B,abort);
        accept(v,B,commit) := accept(v,B,commit) | (b <= B & x(b) = x(B));
    }

    export accept_commit_all_ge
    export vote_or_accept_prepared_all
    export accept_committed
    export do_confirm
    export do_accept
    export vote_commit_between
    export confirm_prepared
    export accept_prepared
    export vote_or_accept_prepared
    
} with value, round, ballot_order, fbas_axiomatization, commit_abort_iso

isolate ballot_protocol = {
    
    specification {
        
        relation vote(N:node, B:ballot, S:statement)
        relation accept(N:node, B:ballot, S:statement)
        relation confirm(N:node, B:ballot, S:statement)

        # ghost vars:
        individual accepted_commit:bool
        individual the_accepted_b:ballot
        
        ## invariants
                
        invariant ~(intact(N1) & intact(N2) & confirm(N1,B1,commit) & confirm(N2,B2,commit) & x(B1) ~= x(B2))
        invariant vote(N,B,S) = abs_actions.vote(N,B,S)
        invariant accept(N,B,S) = abs_actions.accept(N,B,S)
        invariant confirm(N,B,S) = abs_actions.confirm(N,B,S)
        
        after init {
            vote(N,B,S) := false;
            accept(N,B,S) := false;
            confirm(N,B,S) := false;
            accepted_commit := false;
        }
        
        relation condition_accept(N:node, B:ballot, S:statement)
        definition condition_accept(V:node, B:ballot, S:statement) = 
        (exists Q . member(V,Q) & forall N . member(N,Q) -> (vote(N,B,S) | accept(N,B,S)))
        | (exists BL . blocking(V,BL) & forall N . bset_member(N,BL) -> accept(N,B,S))

        relation condition_vote(N:node, B:ballot, S:statement)
        definition condition_vote(V:node, B:ballot, S:statement) =
        forall S2 . ~(vote(V,B,S2) & S ~= S2)

        relation condition_confirm(N:node, B:ballot, S:statement)
        definition condition_confirm(V:node, B:ballot, S:statement) =
        exists Q . member(V,Q) & forall N . member(N,Q) -> accept(N,B,S)
        
        relation condition_accept_tmp(N:node, B:ballot, S:statement)
        relation condition_vote_tmp(N:node, B:ballot, S:statement)
        relation condition_confirm_tmp(N:node, B:ballot, S:statement)
        
        action send_prepare(v:node, b:ballot, p:ballot, pp:ballot, nc:round, nh:round) = {

            condition_accept_tmp(N:node, B:ballot, S:statement) := condition_accept(N,B,S);
            condition_vote_tmp(N:node, B:ballot, S:statement) := condition_vote(N,B,S);
            condition_confirm_tmp(N:node, B:ballot, S:statement) := condition_confirm(N,B,S);
            
	    ## Preconditions:
	    
            require b ~= 0;
            
	    # b can be prepared
	    require ~(B < b & x(B) ~= x(b) & ~(condition_vote(v,B,abort) | condition_accept(v,B,abort)));
	    
	    # preparedPrime is smaller than prepared and has a different value
            require p ~= 0 & pp ~= 0 -> pp < p & p.x ~= pp.x;

	    # p is compatible with b
            require ~(p ~= 0 & b.x ~= p.x);

	    # p is accepted as prepared.
	    require ~(p ~= 0 & B < p & x(B) ~= x(p) & ~condition_accept(v,B,abort));

	    # pp is accepted as prepared.
	    require ~(pp ~= 0 & B < pp & x(B) ~= x(pp) & ~condition_accept(v,B,abort));
	    
	    # if nh ~= 0 then (nh,b.x) is confirmed as prepared
	    # TODO: would accept be enough?
	    var bh:ballot;
	    bh.n := nh;
	    bh.x := b.x;
	    require ~(nh ~= 0 & B < bh & x(B) ~= x(bh) & ~condition_confirm(v, B, abort));
            
            require nc <= nh;

	    # var bc:ballot;
	    # bc.n := nc;
	    # bc.x := b.x;
            # redundant because nc <= nh:
	    # require nh ~= 0 & B < bh & x(B) ~= x(bh) -> condition_confirm(v, B, abort);
            require ~(nc ~= 0 & (nc <= n(B) | n(B) <= nh) & x(B) = x(b) & ~condition_vote(v, B, commit));

	    ## Updates:

	    # vote or accept "prepare(ballot)"
            vote(v, B, abort) := vote(v, B, abort) | (B < b & x(B) ~= x(b) & condition_vote_tmp(v, B, abort));
            accept(v, B, abort) := accept(v, B, abort) | (B < b & x(B) ~= x(b) & condition_accept_tmp(v,B,abort));
            
	    # if "prepared ~= null" accept prepare(p)
	    if (p ~= 0) {
	        accept(v, B, abort) := accept(v, B, abort) | (B < p & x(B) ~= x(p));
	    };
            
	    # if "preparedPrime ~= null" accept prepare(pp)
	    if (pp ~= 0){ 
	            accept(v, B, abort) := accept(v, B, abort) | (B < pp & x(B) ~= x(pp));
	    };

	    # # if "hCounter ~= 0" then confirm (nh,b.x) as prepared
            if (nh ~= 0) {
	        confirm(v, B, abort) := confirm(v, B, abort) | (B < bh & x(B) ~= x(b));
            };
	    
	    # # if "cCounter ~= 0" then vote to commit (N,b.x) for all nc <= B <= nh
	    # var bc:ballot;
	    # bc.n := nc;
	    # bc.x := b.x;
            if (nc ~= 0) {
	        vote(v, B, commit) := vote(v, B, commit) | (nc <= n(B) & n(B) <= nh & x(B) = x(b));
            };
        }
        
        # action do_accept(n:node, b:ballot, s:statement) = {
        #     require intact(n);
        #     require condition_accept(n, b, s);
        #     accept(n, b, s) := true;
        #     call abs_actions.do_accept(n,b,s);
        # }

        # action do_confirm(n:node, b:ballot, s:statement) = {
        #     require intact(n);
        #     require condition_confirm(n, b, s);
        #     confirm(n,b,s) := true;
        #     call abs_actions.do_confirm(n,b,s);
        # }

        export send_prepare
        # export do_accept
        # export do_confirm
        # export send_commit
    }
    
    # export accept_prepared_all_ne_ghost
} with abs_actions, value, round, ballot_order, commit_abort_iso
